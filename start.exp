#!/usr/bin/expect --

set timeout -1

set dirname [file normalize [file dirname $argv0]]

set zomboid_path "zomboid"

proc resolve {args} {
	return [file normalize [file join $::dirname {*}$args]]
}

exec [resolve "steamcmd.sh"] +force_install_dir [resolve $zomboid_path] +runscript [resolve "update_server.scmd"] >@stdout

spawn [resolve $zomboid_path "start-server.sh"] {*}$argv

proc is_empty {} {
	send "players\r"
	expect {
		"Players connected (0)" {return 1}
		"Players connected" {return 0}
	}
}

proc quit_game {} {
	send "quit\r"
	expect eof
}

proc shutdown {} {
	if { [info exists ::env(SHUTDOWN_FIFO)] && ![catch {open $::env(SHUTDOWN_FIFO) {WRONLY NONBLOCK}} fifo]} {
		puts $fifo "quit" 
		close $fifo
	}
}

expect -ex "*** SERVER STARTED ****"

trap {quit_game; exit} {SIGINT SIGTERM}

while {1} {
	puts "looping"
	interact {
		\x03 {quit_game; exit}
		timeout 300 return
		-o -nobuffer "Client connecting" continue
		-nobuffer "Finally disconnected client" return
	}
	if ([is_empty]) {
		interact {
			\x03 {quit_game; exit}
			-nobuffer -re "." continue
			timeout 300 return
			-o -nobuffer "Client connecting" continue
		}
		if {[is_empty]} {
			quit_game
			shutdown
			exit
		}
	}
}